buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        // https://developers.google.com/android/guides/releases
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Note: these plugins are NON-FREE, but unfortunately they cannot be moved into the google-services module.
// Because they have to live in the app module, they will be run even in the F-Droid build flavor, but that shouldn't
// make the app non-free.
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    ndkVersion '23.1.7779620'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        versionCode 118
        versionName "1.3.8"
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        google {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Google Play Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://play.google.com/store/apps/details?id=%s\"'
        }
        amazon {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Amazon App Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"http://www.amazon.com/gp/mas/dl/android?p=%s\"'
        }
        fdroid {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"F-Droid\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://f-droid.org/en/packages/%s\"'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "src/main/jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.4.0'

    // for unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation 'org.robolectric:robolectric:4.7.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'

    implementation project(':common')
    implementation project(':themes')
    implementation project(':game')

    implementation 'androidx.fragment:fragment-ktx:1.4.0'

    // https://github.com/material-components/material-components-android
    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-core-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // https://mvnrepository.com/artifact/androidx.preference/preference
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // https://github.com/ShawnLin013/NumberPicker
    implementation 'io.github.ShawnLin013:number-picker:2.4.13'

    /// NON-FREE DEPENDENCIES, only included in "google" and "amazon" store flavours, excluded in "fdroid"
    /// =====================

    googleImplementation project(':google-services')
    amazonImplementation project(':google-services')
}
