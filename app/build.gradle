plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk = 34
    buildToolsVersion = '34.0.0'

    defaultConfig {
        minSdk 21
        targetSdk 34

        versionCode 158
        versionName "1.5.6"

        resourceConfigurations += ["en", "de", "es", "fr", "ja", "pt", "ro", "ru"]
    }
    flavorDimensions = [ 'app', 'store' ]
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        google {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Google Play Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://play.google.com/store/apps/details?id=%s\"'
        }
        amazon {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Amazon App Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"http://www.amazon.com/gp/mas/dl/android?p=%s\"'
        }
        fdroid {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"F-Droid\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://f-droid.org/en/packages/%s\"'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    buildFeatures {
        buildConfig true
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.8"
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
    namespace 'de.saschahlusiak.freebloks'
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
}

dependencies {
    // for unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation 'org.robolectric:robolectric:4.12.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.5'

    implementation project(':common')
    implementation project(':themes')
    implementation project(':game')
    implementation project(':server')
    implementation project(':ktx')

    implementation 'androidx.fragment:fragment-ktx:1.7.0'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.51.1'
    ksp 'com.google.dagger:hilt-compiler:2.51.1'
    testImplementation 'com.google.dagger:hilt-android-testing:2.51.1'
    kspTest 'com.google.dagger:hilt-android-compiler:2.51.1'

    // https://github.com/material-components/material-components-android
    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation 'com.google.android.material:material:1.12.0'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'

    // Compose
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation 'androidx.compose.material3:material3:1.2.1'
    implementation 'androidx.compose.animation:animation:1.6.7'
    implementation 'androidx.compose.ui:ui-tooling:1.6.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'

    // https://mvnrepository.com/artifact/androidx.preference/preference
    implementation 'androidx.preference:preference-ktx:1.2.1'

    /// NON-FREE DEPENDENCIES, only included in "google" and "amazon" store flavours, excluded in "fdroid"
    /// =====================

    googleImplementation project(':google-services')
    amazonImplementation project(':google-services')
}
