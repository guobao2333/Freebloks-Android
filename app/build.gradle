plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'

    // Note: these plugins are NON-FREE, but unfortunately they cannot be moved into the google-services module.
    // Because they have to live in the app module, they will be run even in the F-Droid build flavor, but that shouldn't
    // make the app non-free.
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'
    ndkVersion '25.0.8775105'

    defaultConfig {
        minSdk 21
        targetSdk 33

        versionCode 122
        versionName "1.4.2"
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        google {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Google Play Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://play.google.com/store/apps/details?id=%s\"'
        }
        amazon {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Amazon App Store\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"http://www.amazon.com/gp/mas/dl/android?p=%s\"'
        }
        fdroid {
            dimension 'store'
            buildConfigField 'String', 'APP_STORE_NAME', '\"F-Droid\"'
            buildConfigField 'String', 'APP_STORE_LINK', '\"https://f-droid.org/en/packages/%s\"'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "src/main/jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'de.saschahlusiak.freebloks'
}

dependencies {
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.5.1'

    // for unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation 'org.robolectric:robolectric:4.8.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'

    implementation project(':common')
    implementation project(':themes')
    implementation project(':game')
    implementation project(':server')

    implementation 'androidx.fragment:fragment-ktx:1.5.3'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.44'
    kapt 'com.google.dagger:hilt-compiler:2.44'
    testImplementation 'com.google.dagger:hilt-android-testing:2.44'
    kaptTest 'com.google.dagger:hilt-android-compiler:2.44'

    // https://github.com/material-components/material-components-android
    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation 'com.google.android.material:material:1.6.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-core-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // https://mvnrepository.com/artifact/androidx.preference/preference
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // https://github.com/ShawnLin013/NumberPicker
    implementation 'io.github.ShawnLin013:number-picker:2.4.13'

    /// NON-FREE DEPENDENCIES, only included in "google" and "amazon" store flavours, excluded in "fdroid"
    /// =====================

    googleImplementation project(':google-services')
    amazonImplementation project(':google-services')
}
